plugins {
  id 'application'
}

group 'com.whitemagicsoftware'
version '1.1'

repositories {
  mavenCentral()
}

dependencies {
  // Command-line parsing
  implementation 'info.picocli:picocli:4.7.0'

  // SVG
  implementation fileTree(include: ['**/*.jar'], dir: 'libs')

  implementation 'com.github.kwhat:jnativehook:2.2.2'
}

sourceSets {
  main {
    java {
      srcDirs = ["src/main/java"]
    }
  }
}

compileJava {
  sourceCompatibility = '14'
  targetCompatibility = '14'

  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

application {
  applicationName = 'kmcaster'
  mainClassName = "com.whitemagicsoftware.${applicationName}.KmCaster"
}

jar {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  manifest {
    attributes 'Main-Class': mainClassName
  }

  from {
    (configurations.runtimeClasspath.findAll { !it.path.endsWith(".pom") }).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }

  archiveFileName = "${applicationName}.jar"

  exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}
